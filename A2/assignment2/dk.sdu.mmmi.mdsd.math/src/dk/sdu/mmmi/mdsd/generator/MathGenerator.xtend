/*
 * generated by Xtext 2.25.0
 */
package dk.sdu.mmmi.mdsd.generator

import dk.sdu.mmmi.mdsd.math.Constant
import dk.sdu.mmmi.mdsd.math.Div
import dk.sdu.mmmi.mdsd.math.Expression
import dk.sdu.mmmi.mdsd.math.MathExp
import dk.sdu.mmmi.mdsd.math.Sub
import dk.sdu.mmmi.mdsd.math.Mul
import dk.sdu.mmmi.mdsd.math.Parenthesis
import dk.sdu.mmmi.mdsd.math.Add
import java.util.HashMap
import java.util.Map
import javax.swing.JOptionPane
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import dk.sdu.mmmi.mdsd.math.VariableBinding
import dk.sdu.mmmi.mdsd.math.VariableUse

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class MathGenerator extends AbstractGenerator {

	//static Map<String, Integer> variables = new HashMap();
	
	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		val variables = resource.allContents.filter(MathExp).next.compute
		
		// You can replace with hovering, see Bettini Chapter 8
		variables.displayPanel
	}
	
	//
	// Compute function: computes value of expression
	// Note: written according to illegal left-recursive grammar, requires fix
	//
		
	def static Map<String, Integer> compute(MathExp math) {
		val variables = new HashMap<String, Integer>()
		
		val tmp = new HashMap<String, Expression>()
		math.exps.forEach[exp | tmp.put(exp.name, exp.exp)]
		
		math.exps.forEach[exp | {
			val res = exp.exp.computeExp(variables, tmp)			
			variables.put(exp.name, res)
		}]
		return variables
	}
	
	def static int computeExp(Expression exp, Map<String, Integer> vars, Map<String, Expression> tmp) {
		switch exp {
			Add: exp.left.computeExp(vars, tmp)+exp.right.computeExp(vars, tmp)
			Sub: exp.left.computeExp(vars, tmp)-exp.right.computeExp(vars, tmp)
			Mul: exp.left.computeExp(vars, tmp)*exp.right.computeExp(vars, tmp)
			Div: exp.left.computeExp(vars, tmp)/exp.right.computeExp(vars, tmp)
			Constant: exp.value
			Parenthesis: exp.exp.computeExp(vars, tmp)
			VariableUse:
			{
				if (!vars.keySet.contains(exp.ref)) {
					val res = tmp.get(exp.ref).computeExp(vars, tmp)
					vars.put(exp.ref, res)
				}
				vars.get(exp.ref)
			}
			VariableBinding: exp.body.computeExp(vars.bind(exp.id, exp.binding.computeExp(vars, tmp)), tmp)
			default: throw new Error("Could not compute expression")
		}
	}
	
	def static Map<String, Integer> bind(Map<String, Integer> vars, String key, Integer value) {
		val binding = new HashMap<String, Integer>(vars)
		binding.put(key, value)
		binding
	}

	def void displayPanel(Map<String, Integer> result) {
		
		var resultString = ""
		if (result.isEmpty()) { 
			resultString = "NO RESULT"
		}
		
		for (entry : result.entrySet()) {
         	resultString += "var " + entry.getKey() + " = " + entry.getValue() + "\n"
        }
		
		JOptionPane.showMessageDialog(null, resultString ,"Math Language", JOptionPane.INFORMATION_MESSAGE)
	}
}
