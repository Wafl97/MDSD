grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

MathExp:
	exps+=Exp*
;

Exp:
	'var' name=ID '=' exp=SumDiff
;

SumDiff returns Expression:
	ProdQuot (('+' {Add.left=current} | '-' {Sub.left=current}) right=ProdQuot)*
;

ProdQuot returns Expression:
	Primary (('*' {Mul.left=current} | '/' {Div.left=current}) right=Primary)*
;

Primary returns Expression:
	Constant | Parenthesis | VariableUse | VariableBinding
;

Parenthesis returns Expression:
	{Parenthesis} '(' exp=SumDiff ')'
;

Constant returns Expression:
	{Constant} value=INT
;

VariableUse returns Expression:
	{VariableUse} ref=ID
;

VariableBinding returns Expression:
	{VariableBinding} 'let' id=ID '=' binding=SumDiff 'in' body=SumDiff 'end'
;